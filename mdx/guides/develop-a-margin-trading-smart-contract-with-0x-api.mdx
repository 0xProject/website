# Create a margin trading smart contract with 0x API

Allow your users to perform complex financial transactions with your Dapp, powered by 0x API. This guide assumes that you are comfortable with the concept of leveraging networked liquidity within a smart contract.

## Overview

Margin trading is a pivotal part of any financial ecosystem; with a smart contract, easily build a trust-less margin position contract with 0x liquidity and Compound Finance money markets.

At a high level, we will be building a smart contract that will have two main functionalities:

1. open a leverage position on ETH with the ETH/DAI market
2. close the leverage position

## Set up your off-chain web3 environment 

We will use our [0x API starter project](https://github.com/0xProject/0x-api-starter-guide-code), which has a basic setup for developing smart contracts and interacting with them on a provided test network. 

<Notification>This project leverages the 0x Core Team‚Äôs smart contract development tooling, which is generally similar to other development tooling like truffle. If there are noteworthy differences, we will highlight them within this guide.</Notification>

Begin by cloning the starter project and installing dependencies:

```sh
$ git clone https://github.com/0xProject/0x-api-starter-guide-code
$ cd 0x-api-starter-guide-code
$ npm install
```

For development, we will be leveraging the Kovan test network and the respective kovan 0x api endpoint: `kovan.api.0x.org/swap/v1/quote`. 

### Configure the .env file

```
ETHEREUM_RPC_URL=<INSERT_RPC_URL_HERE>
MNEMONIC=<INSERT_MNEMONIC_HERE>
NETWORK_ID=42
```

For the mnemonic, because we are using the Kovan network, use an account that has some amount of Kovan ETH (KETH). Acquire KETH from a [faucet](https://faucet.kovan.network).

We will be interacting mainly with the `examples/` folder‚Äôs `.ts` files; they are scripts that represent web3 interactions with your smart contract that one may find in a Dapp‚Äôs front end. For this guide, we will be mainly concerned with the `examples/smart_contract_margin_trading.ts` file.

## Writing a margin position smart contract

Before we begin writing the contract, let's break down what happens when we open a position.

Open a position:

1. Send the amount of ETH to the contract to leverage.
2. Deposit the ETH (on the user's behalf) in Compound Finance ETH pool.
3. With the deposited ETH, we can borrow DAI, in this example that amount is set by the `sellAmount` provided by the 0x quote.
4. With the DAI borrowed, sell for WETH
5. Update contract state to monitor position size

Note: The `sellAmount` amount is chosen off-chain and must be under the collateral ratio set by Compound Finance. (For ETH, you can only borrow 75% of the value supplied)

With the pseudo-code above, let's begin setting up the smart contract. For the sake of simplicity and minimal state management, we will be writing a smart contract that handles only one open position at a time. `positionBalance` will be how we maintain the total ETH (in Compound Finance, and WETH) held by the smart contract. With a few modifiers, we can monitor the state of the position held by the smart contract.

### Contract Constructor

Because we will be interacting with a number of Defi protocols, we will need a few references to the varying protocol's smart contracts. We will need to initialize our smart contract with all the references passed at deployment.

```solidity
// contracts/SimpleMarginTrading.sol
...
constructor (
    address _exchange,
    address _comptroller,
    address _cdai,
    address _dai,
    address payable _ceth,
    address _weth
    )
    public
{
    exchange = IExchange(_exchange);
    comptroller = IComptroller(_comptroller);
    cdai = ICERC20(_cdai);
    ceth = ICEther(_ceth);
    weth = IEtherToken(_weth);
    dai = IERC20Token(_dai);

    owner = msg.sender;

    // Enter markets
    _enterMarkets();
}
...
```

Let's break down a few of the references we are going to be interacting with:


1. `exchange`: the main 0x protocol smart contract to interact with
2. `comptroller`: the main compound finance smart contract to interact with
3. `cdai`, `ceth`: 'c' tokens used by compound finance to represent assets in the protocol and accrued interests

`_enterMarkets()` is a function that allows the contract to interact with Compound Finance for the ETH and DAI money markets.

### Verifying and executing a 0x swap quote on chain 

Unlike previous guides that execute 0x transactions on-chain without additional verification (assuming the provided quote is valid), we will take the extra steps to verify that token balances changed as intended from executing a swap.

To do that, we will be writing a util function that executes and verifies the result of performing a swap:

```
    // contracts/SimpleMarginTrading.sol
    function _executeAndVerifyZeroExSwap(ZeroExQuote memory quote)
        internal
        returns (LibFillResults.FillResults memory fillResults)
    {
        uint256 sellTokenBalanceBefore = IERC20Token(quote.sellToken).balanceOf(address(this));
        uint256 buyTokenBalanceBefore = IERC20Token(quote.buyToken).balanceOf(address(this));
        (bool success, bytes memory data) = address(exchange).call.value(quote.protocolFee)(quote.calldataHex);
        require(success, "Swap not filled");
        fillResults = abi.decode(data, (LibFillResults.FillResults));
        uint256 sellTokenBalanceAfter = IERC20Token(quote.sellToken).balanceOf(address(this));
        uint256 buyTokenBalanceAfter = IERC20Token(quote.buyToken).balanceOf(address(this));
        // check the balances of the respective sell and buy token balances changed as intended by executing the swap.
        require(sellTokenBalanceBefore.safeSub(sellTokenBalanceAfter) == fillResults.takerAssetFilledAmount, "Swap performed did not sell correct token/token amount");
        require(buyTokenBalanceAfter.safeSub(buyTokenBalanceBefore) == fillResults.makerAssetFilledAmount, "Swap performed did not sell correct token/token amount");
    }
```

This function validates that the token balances of both `sellToken` and `buyToken` changed by the amount returned in `fillResults`.

### Opening a position

The function signature for the `open` function is defined as so:

```solidity
// contracts/SimpleMarginTrading.sol
function open(
    ZeroExQuote memory quote,
    address allowanceTarget
)
    public
    payable
    onlyowner
    onlyWhenClosed
    returns (uint256 positionBalance, uint256 borrowBalance)
{
...
}
```

The function will only allow the `owner` to open a position and only when the smart contract has no currently opened positions. The parameter `quote` will contain the 0x API response to marketBuy WETH with DAI.

#### Determining amount of ETH to deposit into Compound finance

With the `protocolFee` introduced in version 3 of 0x protocol, some of the ETH sent along the transaction will be reserved to pay the `protocolFee`. Before we deposit ETH into Compound finance, we will need to calculate how much of `msg.value` we can deposit.

```solidity
function open(
    ZeroExQuote memory quote,
    address allowanceTarget
)
  public
  payable
  onlyOwner
  onlyWhenClosed
  returns (uint256 totalPositionBalance, uint256 borrowBalance)
{
    ...
    // 1. increase position by msg.value - protocolFeeAmount
    positionBalance = msg.value.safeSub(quote.protocolFeeAmount);
}
```

#### Deposit and borrow DAI

With `positionBalance`, we can then deposit and mint cETH with Compound Finance.

```solidity
    ...
    // 2. mint collateral in compound
    ceth.mint.value(positionBalance)();
    ...
```

Now that we have `accountLiquidity` (borrowing power in Compound Finance), let's borrow DAI from Compound Finance. We will be borrowing the exact amount of DAI we intend to sell with 0x liquidity. Off-chain, `sellAmount` is calculated based on our desired leverage.

```
    ...
    // 3. borrow token
    require(cdai.borrow(quote.sellAmount) == 0, "Failed to borrow cDAI from Compound Finance");
    ...
```

Compound finance returns a non-zero value if an error occurs during the function call, with a `require` we can proceed with the function only when the function doesn't throw any errors.

#### Swaping for ETH with 0x liquidity 

To swap with 0x, the 0x exchange contracts will need to have access to move DAI from the `MarginPositionContract`. With the provided functions in the starter code, let‚Äôs approve `MAX_AMOUNT` of DAI: 

```solidity
    ...
    // 4. approve 0x exchange to move DAI
    _approve(address(dai), allowanceTarget);
    ...
```

Since the quote is passed off-chain, it is worth verifying that the assets you intend to sell and buy.

```solidity
    ...
    // 5. verify quote is valid
    require(quote.sellToken == address(weth), "Provided quote is not selling WEth");
    require(quote.buyToken == address(dai), "Provided quote is not buying Dai");
    ...
```

Now that we have verified and set the approvals to perform a swap with 0x protocol, let‚Äôs execute the swap with the util function `_executeAndVerifyZeroExSwap`.

```solidity
    ...
    // 6. execute swap
    LibFillResults.FillResults memory fillResults = _executeAndVerifyZeroExSwap(quote);
    ...
```

#### Updating the contract state

With that, we have just swapped DAI for WETH, effectively opening our margin position. Before we finish this function, let‚Äôs update the contract state and return some results to the user. From the 0x contract call, we have a receipt that contains the actual amount of buy and sell token swapped.

```solidity
    ...
    // 8. position size increase by bought amount of WETH
    positionBalance.safeAdd(fillResults.makerAssetFilledAmount);
    totalPositionBalance = positionBalance;
    borrowBalance = cdai.borrowBalanceCurrent(address(this));
    // at this point you have ceth, and swapped for WETH
}
```

At this point, the open function should look like this:

```solidity
    // contracts/SimpleMarginTrading.sol
    function open(
        ZeroExQuote memory quote,
        address allowanceTarget
    )
        public
        payable
        onlyOwner
        onlyWhenClosed
        returns (uint256 totalPositionBalance, uint256 borrowBalance)
    {
        // 1. increase position by msg.value - protocolFee
        positionBalance = msg.value.safeSub(quote.protocolFee);
        // 2. mint collateral in compound
        ceth.mint.value(positionBalance)();
        // 3. borrow token
        require(cdai.borrow(quote.sellAmount) == 0, "Failed to borrow cDAI from Compound Finance");
        // 4. approve 0x exchange to move DAI
        _approve(address(dai), allowanceTarget);
        // 5. execute and verify swap
        require(quote.sellToken == address(weth), "Provided quote is not selling WEth");
        require(quote.buyToken == address(dai), "Provided quote is not buying Dai");
        LibFillResults.FillResults memory fillResults = _executeAndVerifyZeroExSwap(quote);
        // 6. position size increase by bought amount of WETH
        positionBalance = positionBalance.safeAdd(fillResults.makerAssetFilledAmount);
        totalPositionBalance = positionBalance;
        borrowBalance = cdai.borrowBalanceCurrent(address(this));
        // at this point you have cETH, and swapped for WETH
    }
```

### Closing a position

In many ways, closing a position is reversing the actions of opening a position.

Close a position:

1. With the WETH held by contract, swap for the amount of DAI borrowed + interest.
2. Pay back the `borrowBalance` with DAI
3. Redeem (withdraw) the ETH in compound finance
4. Unwrap all the WETH remaining (if any) to ETH
5. Transfer all the ETH back to the user

If ETH has risen in price, then you will have more remaining WETH after buying DAI.

Let‚Äôs begin by defining the function signature for `close`:

```solidity
function close(
    ZeroExQuote memory quote,
    address allowanceTarget
)
    public
    payable
    onlyOwner
    onlyWhenOpen
    returns (uint ethBalance)
{
...
}
```

Intuitively, it makes sense to only be able to close a position if there is one open, hence the modifier `onlyWhenOpen`.

#### Buying DAI back.

To pay back acrued interest from borrowing DAI from Compound Finance, we will wrap additional `msg.value` as WETH to acquire extra DAI to pay off `borrowBalance`. Leftover WETH will be returned to the user.

```solidity
    // 1. wrap extra ETH sent with tx to buy DAI (additional ETH is sent to pay for interest rate)
    weth.deposit.value(msg.value.safeSub(quote.protocolFee))();
```

Similar to swapping from DAI to WETH in `open`, we will need to do a few things before swapping:

```solidity
    ...
    // 2. approve 0x exchange to move WETH
    _approve(address(weth), allowanceTarget);
    // 3. verify swap amounts are enough to repay balance
    uint256 wethBalance = weth.balanceOf(address(this));
    uint256 daiBorrowBalance = cdai.borrowBalanceCurrent(address(this));
    require(wethBalance >= quote.sellAmount, "Provided quote exceeds available WETH");
    require(daiBorrowBalance <= quote.buyAmount, "Provided quote doesn't provide sufficient liquidity");
    ...
```

Here, we are approving 0x contracts and verifying that the quote provided is trading the right assets and has valid sell and buy amounts. Off-chain, we will be calculating the exact amount of DAI (`buyAmount`) to buy.

Once verified, let‚Äôs perform the swap:

```solidity
    ...
    // 4. execute and verify swap
    _executeAndVerifyZeroExSwap(quote);
    ...
```

At this point, the contract should have a balance of `daiBorrowBalance` DAI.

#### Repay borrowed DAI and withdraw ETH

With the DAI bought with 0x API, let‚Äôs repay back our `borrowBalance` on Compound finance:

```solidity
    ...
    // 5. return back dai
    _approve(address(dai), address(cdai));
    require(cdai.repayBorrow(daiBorrowBalance) == 0, "Repayment of DAI to Compound Finance failed");
    ...
```

To return DAI back, an approval for the `cdai` contract is needed. With `daiBorrowBalance` we can repay the exact amount of DAI borrowed + interest. Once, `borrowBalance` is repaid, let‚Äôs withdraw the ETH deposited in `ceth` by redeeming the contract's cETH.

```solidity
    ...
    // 6. get back ETH
    require(ceth.redeem(ceth.balanceOf(address(this))) == 0, "Withdrawal of ETH from Compound Finance failed");
    ...
```

At this point, the contract holds ETH and WETH (whatever WETH remains not swapped for DAI). For the sake of UX, we will be unwrapping WETH to ETH and transferring all the ETH back to the owner. Then let‚Äôs reset `positionBalance` and allow another position to be opened.

```solidity
    ...
    // 7. withdraw all WETH Balance;
    weth.withdraw(weth.balanceOf(address(this)));
    // 8. transfer all ETH back to owner;
    ethBalance = address(this).balance;
    owner.transfer(address(this).balance);
    // 9. reset balance
    positionBalance = 0;
}
```

At this point `close` function should look something like this:

```solidity
    function close(
       ZeroExQuote memory quote,
       address allowanceTarget
    )
        public
        payable
        onlyOwner
        onlyWhenOpen
        returns (uint ethBalance)
    {
        // 1. wrap extra ETH sent with tx to buy DAI (additional ETH is sent to pay for interest rate)
        weth.deposit.value(msg.value.safeSub(quote.protocolFee))();
        // 2. approve 0x exchange to move WETH
        _approve(address(weth), allowanceTarget);
        // 3. verify swap amounts are enough to repay balance
        uint256 wethBalance = weth.balanceOf(address(this));
        uint256 daiBorrowBalance = cdai.borrowBalanceCurrent(address(this));
        require(quote.sellToken == address(weth), "Provided quote is not selling WEth");
        require(quote.buyToken == address(dai), "Provided quote is not buying Dai");
        require(wethBalance >= quote.sellAmount, "Provided quote exceeds available WETH");
        require(daiBorrowBalance <= quote.buyAmount, "Provided quote doesn't provide sufficient liquidity");
        // 4. execute and verify swap
        _executeAndVerifyZeroExSwap(quote);
        // 5. return back DAI
        _approve(address(dai), address(cdai));
        require(cdai.repayBorrow(daiBorrowBalance) == 0, "Repayment of DAI to Compound Finance failed");
        // 6. get back ETH
        require(ceth.redeem(ceth.balanceOf(address(this))) == 0, "Withdrawal of ETH from Compound Finance failed");
        // 7. withdraw all WETH Balance;
        weth.withdraw(weth.balanceOf(address(this)));
        // 8. transfer all ETH back to owner;
        ethBalance = address(this).balance;
        owner.transfer(address(this).balance);
        // 9. reset balance
        positionBalance = 0;
    }
```

### Adding some handy functions.

To make interacting with the margin contract easier with web3, let‚Äôs add a public function that returns the contracts DAI `borrowBalance`.

```solidity
    // handy function to get borrowed dai amount
    function getBorrowBalance()
        public
        onlyowner
        returns (uint256)
    {
        return cdai.borrowBalanceCurrent(address(this));
    }
```

With that, we are done with the contract! Let‚Äôs write some web3 code to interact with the contract just written. Before we do so, let‚Äôs compile the contracts and build typescript contract-wrappers:

```sh
$ npm run build:contracts
```

## Interacting with the contract

Prepared in the getting started code is an `example/smart_contract_margin_trading.ts` script that allows you to easily interact with the contract written. We will be working on finishing the open and close functions.

### Opening the position in web3

We will be fleshing out the `openAsync` function to fetch a quote from 0x API and execute a contract call with our margin trading contract.

```typescript
// examples/smart_contract_margin_trading.ts
const openAsync = async (web3Wrapper: Web3Wrapper, contract: SimpleMarginTradingContract) => {
    ...
}
```

Let‚Äôs begin by calculating the `positionSize` and amount of leverage:

```typescript
...
// calculations for margin trading
const positionSize = baseUnitAmount(0.1);
const leverage = 0.5; // 1.5x leverage 
const buyAmount = positionSize.multipliedBy(leverage);
...
```

With `buyAmount` we can fetch a quote from 0x API to buy WETH:

```typescript
...
const params = {
    buyToken: 'ETH',
    sellToken: 'DAI',
    excludedSources: 'Eth2Dai,Uniswap,Kyber',
}

const res = await fetch(`https://kovan.api.0x.org/swap/v1/quote?${qs.stringify(params)}`);
const quote = await res.json();
...
```

The quote will allow us to buy `buyAmount` DAI with ETH. To prepare the response provided for consumption on-chain, let‚Äôs reformat the quote response to the key value pairs used on-chain.

```typescript
// prepare API response for contract use
const onchainPassableQuote = {
    buyToken: WETH_CONTRACT,
    sellToken: DAI_CONTRACT,
    buyAmount: quote.buyAmount,
    sellAmount: quote.sellAmount,
    protocolFee: quote.protocolFee,
    calldataHex: quote.data,
};
const value = positionSize.plus(quote.protocolFee);
const allowanceTarget = quote.allowanceTarget;
```

Since we will be using ETH to pay protocol fees with 0x, we will need to send additional ETH with the transaction along with the `positionSize`. With the above variables, let‚Äôs call the smart contract.

```typescript
// 3. execute a smart contract call to open a margin position
try {
    const results = await contract.open(onchainPassableQuote, allowanceTarget).callAsync({
        from: takerAddress,
        value,
        gasPrice: quote.gasPrice,
        gas: 3000000,
    });

    console.log(`position size: (ETH in Compound + WETH): ${results[0]}`);
    console.log(`dai borrowed from Compound: ${results[1]}`);
    
    await contract.open(onchainPassableQuote, allowanceTarget).awaitTransactionSuccessAsync({
        from: takerAddress,
        value,
        gasPrice: quote.gasPrice,
        gas: 3000000,
    });
    console.log("opened position.");
} catch (e) {
    throw e;
}
```

We have called the contract with `callAsync` to see the return results of performing a margin trade before actually executing it with `awaitTransactionSuccessAsync`:

```
position size: (ETH in Compound + WETH): 150000000000000000
dai borrowed from Compound: 8998532000000000000
```

Th DAI borrowed should be approximately half the value of ETH in DAI. With that, you have just opened a position with the margin trading contract. 

Take a break, grab some matcha üçµ. We are almost done with this guide!


### Closing the position in web3

We will be fleshing out the `closeAsync` function to fetch a quote from 0x API and execute a contract call with our margin trading contract.

```typescript
// examples/smart_contract_margin_trading.ts
const closeAsync = async (web3Wrapper: Web3Wrapper, contract: SimpleMarginTradingContract) => {
    ...
}
```

From the contract, we will need to know the DAI borrow balance to create an accurate quote. We will be using the helper function we created earlier to get the `daiBorrowBalance`:

```typescript
...
const daiBorrowBalance = (await contract.getBorrowBalance().callAsync({
    from: takerAddress,
})).times(1.0000001).integerValue();
...
```

Since interest is accrued every block, by contract call on-chain, the interest would have accrued by some amount, for the sake of simplicity in the guide, we will be acquiring a little bit more DAI.

Similar to `openAsync` let‚Äôs fetch and prepare a quote with `daiBorrowBalance` set as the `buyAmount`.

```typescript
...
const params = {
    buyToken: 'DAI',
    sellToken: 'WETH',
    buyAmount: daiBorrowBalance.toString(),
}

const res = await fetch(`https://api.0x.org/swap/v1/quote?${qs.stringify(params)}`);
const quote = await res.json();

const onchainPassableQuote = {
    buyToken: WETH_CONTRACT,
    sellToken: DAI_CONTRACT,
    buyAmount: quote.buyAmount,
    sellAmount: quote.sellAmount,
    protocolFee: quote.protocolFee,
    calldataHex: quote.data,
};

...
```

To pay the additional interest from borrowing, we will be sending some more ETH along the protocolFee to pay the interest rate.

```typescript
...
// 3. calculate and provide an extra buffer of WETH to pay interest accrued.
const extraWethBuffer = new BigNumber(quote.sellAmount).times(0.01);
const value = (new BigNumber(quote.protocolFee)).plus(extraWethBuffer).integerValue();
const allowanceTarget = quote.allowanceTarget;
...
```


Now, let‚Äôs call the smart contract: 

```typescript
...
// 4. execute a smart contract call to open a margin position
try {
    const results = await contract.close(onchainPassableQuote, allowanceTarget).callAsync({
        from: takerAddress,
        value,
        gasPrice: quote.gasPrice,
        gas: 9000000,
    });

    console.log(`eth balance size after closing position: ${results}`);

    await contract.close(onchainPassableQuote, allowanceTarget).awaitTransactionSuccessAsync({
        from: takerAddress,
        value,
        gasPrice: quote.gasPrice,
        gas: 9000000,
    });
    console.log("closed position.");
} catch (e) {
    throw e;
}
```

With that, we have just closed the margin position! You can see the total ETH returned to the user with the return value from `.close()`.

Run the script to test your smart contract:

```sh
$ npm run test:margin_trading
```

## Wrapping Up
Just like that, you wrote a margin trading smart contract with 0x + Compound. Keep in mind this is a rather primitive example of a margin trading experience. There is a number of follow ups a developer can do with the code written here:

* liquidation prevention functions to ‚Äútop up‚Äù the collateral provided to Compound
* better handling of interest accrued with flash loans
* calldata decoding/encoding for provided swap quote
* better error handling
* manage multiple positions with one contract

<Notification type="alert">The margin trading contract is unaudited and is NOT recommended to be use on mainnet with real ETH.</Notification>

If you want to take the ideas brought up to the next level, we have a number of resources you can leverage (haha get it?):

* Refer to our [0x API specification](https://0x.org/docs/api) for detailed documentation
* 0x API is open source! Look through the codebase and deploy your own 0x API instance.


